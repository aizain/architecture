group 'com.gyzh.zain.lg.first'
version '1.0-SNAPSHOT'

buildscript { // 构建项目时用的
    ext {
        kotlin_version = '1.1.4-2'
        shadow_version = '2.0.1'
    }

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:$shadow_version"
    }
}

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application' // 用于运行
apply plugin: 'com.github.johnrengelman.shadow' // 用于打包

sourceCompatibility = 1.8
mainClassName = 'io.vertx.core.Launcher'
def mainVerticleName = 'com.gyzh.zain.vertx.web.BootstrapVerticle' // 声明入口
def watchForChange = 'src/**/*'
def doOnChange = './gradlew classes'

def vertx_version = '3.4.2'
def logback_version = '1.2.3'

// 下面是项目中用的
repositories {
    jcenter()
    maven {
        url "https://oss.sonatype.org/content/repositories/iovertx-3684/"
    }
}

dependencies {
    // vertx
    compile "io.vertx:vertx-core:$vertx_version"
    compile "io.vertx:vertx-web:$vertx_version"
    compile "io.vertx:vertx-auth-shiro:$vertx_version"
    compile "io.vertx:vertx-mysql-postgresql-client:$vertx_version"
    compile "io.vertx:vertx-web-templ-jade:$vertx_version"

    // kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

    // logger
    compile "ch.qos.logback:logback-classic:$logback_version"

    // test
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile "io.vertx:vertx-unit:$vertx_version"
}


compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

shadowJar {
    classifier = 'fat'
    manifest {
        attributes "Main-Verticle": mainVerticleName
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

run {
    args = [
        "run"
        , mainVerticleName
        , "--redeploy=$watchForChange"
        , "--launcher-class=$mainClassName"
        , "--on-redeploy=$doOnChange"
    ]
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0'
}